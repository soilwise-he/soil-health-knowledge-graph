PREFIX she: <https://soilwise-he.github.io/soil-health#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX schema: <https://schema.org/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX sorelph: <http://sweetontology.net/relaPhysical/>

SELECT DISTINCT 
    ?mainCategoryLabel
    ?methodLabel
    ?specificIndicatorLabel 
    ?relationshipType ?relatedEntityLabel
WHERE {
    she:BiologicalIndicators dcterms:source she:Ritz2009 ;
                            skos:narrower ?mainCategory .
    
    ?mainCategory skos:prefLabel ?mainCategoryLabel .
    
    ?mainCategory skos:narrower ?specificIndicator .
    ?specificIndicator skos:prefLabel ?specificIndicatorLabel .
    
    OPTIONAL {
        ?mainCategory schema:isBasedOn ?method .
        ?method skos:prefLabel ?methodLabel .
    }
    
    OPTIONAL {
        {
            ?specificIndicator obo:RO_0002507 ?relatedEntity .
            ?relatedEntity skos:prefLabel ?relatedEntityLabel .
            BIND(obo:RO_0002507 AS ?relationshipType)
        }
        UNION
        {
            ?specificIndicator skos:related ?relatedEntity .
            ?relatedEntity skos:prefLabel ?relatedEntityLabel .
            BIND(skos:related AS ?relationshipType)
        }
        UNION
        {
            ?specificIndicator she:indicates ?relatedEntity .
            ?relatedEntity skos:prefLabel ?relatedEntityLabel .
            BIND(she:indicates AS ?relationshipType)
        }
        UNION
        {
            ?specificIndicator sorelph:from ?relatedEntity .
            ?relatedEntity skos:prefLabel ?relatedEntityLabel .
            BIND(sorelph:from AS ?relationshipType)
        }
    }

    FILTER EXISTS {
        ?mainCategory dcterms:source she:Ritz2009
    }
}
ORDER BY ?mainCategoryLabel ?specificIndicatorLabel