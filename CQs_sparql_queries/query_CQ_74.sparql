PREFIX she: <https://soilwise-he.github.io/soil-health#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX agrontology: <http://aims.fao.org/aos/agrontology#>
PREFIX dcterms: <http://purl.org/dc/terms/>

SELECT DISTINCT ?categoryLabel ?relationshipType ?relatedEntityLabel
WHERE {
  {
    she:SoilSamplingSites obo:T4FS_0000577 ?relatedEntity .
    she:SoilSamplingSites skos:prefLabel ?categoryLabel .
    ?relatedEntity skos:prefLabel ?relatedEntityLabel .
    FILTER(?relatedEntity != she:StandardDocumentation)
    BIND(obo:T4FS_0000577 AS ?relationshipType)
  }
  UNION
  {
    she:SoilSamplingSites obo:T4FS_0000577 ?category .
    ?category skos:prefLabel ?categoryLabel .
    ?category agrontology:hasProperty ?relatedEntity .
    ?relatedEntity skos:prefLabel ?relatedEntityLabel .
    BIND(agrontology:hasProperty AS ?relationshipType)
  }
  UNION
  {
    she:SoilSamplingSites obo:T4FS_0000577 ?category .
    ?category skos:narrower ?relatedEntity .
    ?relatedEntity skos:prefLabel ?relatedEntityLabel .
    ?category skos:prefLabel ?categoryLabel .
    BIND(skos:narrower AS ?relationshipType)
  }
  UNION
  {
    she:SoilSamplingSites obo:T4FS_0000577 ?category .
    ?category skos:narrower ?relatedEntity .
    ?relatedEntity skos:narrower ?narrowEntity .
    ?narrowEntity skos:prefLabel ?relatedEntityLabel .
    ?relatedEntity skos:prefLabel ?categoryLabel .
    BIND(skos:narrower AS ?relationshipType)
  }
  UNION
  {
    she:SoilSamplingSites obo:T4FS_0000577 ?category .
    ?category skos:narrower ?relatedEntity .
    ?relatedEntity skos:narrower ?narrowEntity .
    ?narrowEntity skos:definition ?relatedEntityLabel .
    ?narrowEntity skos:prefLabel ?categoryLabel .
    BIND(skos:definition AS ?relationshipType)
  }
}
ORDER BY ?categoryLabel ?relationshipType ?relatedEntityLabel