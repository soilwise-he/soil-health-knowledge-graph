PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX she: <https://soilwise-he.github.io/soil-health#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX agrontology: <http://aims.fao.org/aos/agrontology#>
PREFIX af-x: <http://purl.allotrope.org/ontologies/property#>
PREFIX sorelm: <http://sweetontology.net/relaMath/>

SELECT ?subjectLabel ?predicateURI ?objectLabel
WHERE {
  {
    she:SoilRigidityRatio skos:prefLabel ?srrLabel .
    she:SoilRigidityRatio skos:definition ?definition .
    BIND(?srrLabel AS ?subjectLabel)
    BIND(skos:definition AS ?predicateURI)
    BIND(?definition AS ?objectLabel)
  }
  UNION
  {
    she:SoilRigidityRatio skos:prefLabel ?srrLabel_measured .
    she:SoilRigidityRatio agrontology:isMeasuredBy ?measureNode .
    ?measureNode obo:RO_0020202 ?aps .
    ?measureNode obo:RO_0020203 ?ass .
    ?aps skos:prefLabel ?apsLabel_measure .
    ?ass skos:prefLabel ?assLabel_measure .

    BIND(?srrLabel_measured AS ?subjectLabel)
    BIND(agrontology:isMeasuredBy AS ?predicateURI)
    BIND(CONCAT(?apsLabel_measure, " / ", ?assLabel_measure) AS ?objectLabel)
  }
  UNION
  {
    {
      she:SoilRigidityRatio skos:prefLabel ?srrLabel_gt .
      she:SoilRigidityRatio sorelm:hasThreshold ?thresholdNode_gt .
      ?thresholdNode_gt agrontology:greaterThan ?threshold12 .
      ?thresholdNode_gt af-x:AFX_0002842 ?rssc .
      ?rssc skos:prefLabel ?rsscLabel .
      BIND(CONCAT(?srrLabel_gt, " > ", STR(?threshold12)) AS ?subjectLabel)
      BIND(af-x:AFX_0002842 AS ?predicateURI)
      BIND(?rsscLabel AS ?objectLabel)
    }
    UNION
    {
      she:SoilRigidityRatio skos:prefLabel ?srrLabel_gt .
      she:SoilRigidityRatio sorelm:hasThreshold ?thresholdNode_gt .
      ?thresholdNode_gt agrontology:greaterThan ?threshold12 .
      ?thresholdNode_gt af-x:AFX_0002727 ?cp .
      ?cp skos:prefLabel ?cpLabel_gt .
      BIND(CONCAT(?srrLabel_gt, " > ", STR(?threshold12)) AS ?subjectLabel)
      BIND(af-x:AFX_0002727 AS ?predicateURI)
      BIND(?cpLabel_gt AS ?objectLabel)
    }
  }
  UNION
  {
    she:SoilRigidityRatio skos:prefLabel ?srrLabel_lt .
    she:SoilRigidityRatio sorelm:hasThreshold ?thresholdNode_lt .
    ?thresholdNode_lt agrontology:smallerThan ?threshold08 .
    ?thresholdNode_lt af-x:AFX_0002842 ?ids .
    ?ids skos:prefLabel ?idsLabel .

    BIND(CONCAT(?srrLabel_lt, " < ", STR(?threshold08)) AS ?subjectLabel)
    BIND(af-x:AFX_0002842 AS ?predicateURI)
    BIND(?idsLabel AS ?objectLabel)
  }
  UNION
  {
    {
      she:SoilRigidityRatio skos:prefLabel ?srrLabel_interval .
      she:SoilRigidityRatio sorelm:hasThreshold ?thresholdNode_interval .
      ?thresholdNode_interval sorelm:hasInterval ?interval .
      ?thresholdNode_interval agrontology:causes ?sd .
      ?sd skos:prefLabel ?sdLabel_interval .
      BIND(CONCAT(?srrLabel_interval, " ", STR(?interval)) AS ?subjectLabel)
      BIND(agrontology:causes AS ?predicateURI)
      BIND(?sdLabel_interval AS ?objectLabel)
    }
    UNION
    {
      she:SoilRigidityRatio skos:prefLabel ?srrLabel_interval .
      she:SoilRigidityRatio sorelm:hasThreshold ?thresholdNode_interval .
      ?thresholdNode_interval sorelm:hasInterval ?interval .
      ?thresholdNode_interval obo:RO_0002552 ?affectedEntity_interval .
      ?affectedEntity_interval skos:prefLabel ?affectedEntityLabel_interval .
      BIND(CONCAT(?srrLabel_interval, " ", STR(?interval)) AS ?subjectLabel)
      BIND(obo:RO_0002552 AS ?predicateURI)
      BIND(?affectedEntityLabel_interval AS ?objectLabel)
    }
  }
}
ORDER BY ?subjectLabel ?predicateURI ?objectLabel