PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX she: <https://soilwise-he.github.io/soil-health#>
PREFIX agrontology: <http://aims.fao.org/aos/agrontology#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX sorelph: <http://sweetontology.net/relaPhysical/>
PREFIX af-x: <http://purl.allotrope.org/ontologies/property#>

SELECT ?subjectLabel ?predicateURI ?objectLabel ?contextLabel
WHERE {
  {
    she:InternalSoilStrength skos:prefLabel ?issLabel_eq .
    she:InternalSoilStrength sio:equivalentTo ?aps .
    ?aps skos:prefLabel ?apsLabel .

    BIND(?issLabel_eq AS ?subjectLabel)
    BIND(sio:equivalentTo AS ?predicateURI)
    BIND(?apsLabel AS ?objectLabel)
  }
  UNION
  {
    she:AdditionalStress skos:prefLabel ?asLabel_hm .
    she:AdditionalStress sorelph:from ?hm .
    ?hm skos:prefLabel ?hmLabel .

    BIND(?asLabel_hm AS ?subjectLabel)
    BIND(sorelph:from AS ?predicateURI)
    BIND(?hmLabel AS ?objectLabel)
  }
  UNION
  {
    she:AdditionalStress skos:prefLabel ?asLabel_drv .
    she:AdditionalStress sorelph:from ?drivingNode .
    ?drivingNode rdf:object ?driving .
    ?driving skos:prefLabel ?drivingLabel .
    ?drivingNode af-x:AFX_0000408 ?hsmc .
    ?hsmc skos:prefLabel ?hsmcLabel .

    BIND(?asLabel_drv AS ?subjectLabel)
    BIND(sorelph:from AS ?predicateURI)
    BIND(?drivingLabel AS ?objectLabel)
    BIND(CONCAT("when ", ?hsmcLabel) AS ?contextLabel)
  }
  UNION
  {
    she:SoilCompaction skos:prefLabel ?scLabel_def .
    she:SoilCompaction skos:definition ?scdefinition .

    BIND(?scLabel_def AS ?subjectLabel)
    BIND(skos:definition AS ?predicateURI)
    BIND(?scdefinition AS ?objectLabel)
  }
  UNION
  {
    she:PrecompressionStress skos:prefLabel ?psLabel_def .
    she:PrecompressionStress skos:definition ?psdefinition .

    BIND(?psLabel_def AS ?subjectLabel)
    BIND(skos:definition AS ?predicateURI)
    BIND(?psdefinition AS ?objectLabel)
  }
}
ORDER BY ?subjectLabel ?predicateURI ?objectLabel