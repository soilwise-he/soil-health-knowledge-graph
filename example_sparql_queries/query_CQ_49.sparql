PREFIX agrontology: <http://aims.fao.org/aos/agrontology#>
PREFIX she: <https://soilwise-he.github.io/soil-health#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sorelph: <http://sweetontology.net/relaPhysical/>

SELECT DISTINCT ?pollutantLabel ?relationship ?sourceLabel
WHERE {
  {
    ?pollutant sorelph:from ?source .
    ?pollutant skos:prefLabel ?pollutantLabel .
    ?source skos:prefLabel ?sourceLabel .
    BIND(sorelph:from AS ?relationship)
    
    FILTER EXISTS {
      ?pollutant agrontology:isIncludedIn she:EmergingConcernPollutants
    }
  }
  UNION
  {
    ?pollutant sio:SIO_000128 ?source .
    ?pollutant skos:prefLabel ?pollutantLabel .
    ?source skos:prefLabel ?sourceLabel .
    BIND(sio:SIO_000128 AS ?relationship)
    
    FILTER EXISTS {
      ?pollutant agrontology:isIncludedIn she:EmergingConcernPollutants
    }
  }
  UNION
  {
    ?broadCategory agrontology:includes ?pollutant .
    ?broadCategory sio:SIO_000128 she:ArableSoils .
    ?broadCategory skos:prefLabel ?pollutantLabel .
    ?pollutant skos:prefLabel ?sourceLabel .
    BIND(agrontology:includes AS ?relationship)
  }
  UNION
  {
    ?pollutant skos:narrower ?specificPollutant .
    ?pollutant sio:SIO_000128 she:ArableSoils .
    ?pollutant skos:prefLabel ?pollutantLabel .
    ?specificPollutant skos:prefLabel ?sourceLabel .
    BIND(skos:narrower AS ?relationship)
  }
  UNION
  {
    ?metal skos:broader she:Metals .
    she:Metals sio:SIO_000128 she:ArableSoils .
    ?metal sio:SIO_000128 ?source .
    ?metal skos:prefLabel ?pollutantLabel .
    ?source skos:prefLabel ?sourceLabel .
    FILTER(?source != she:ArableSoils)
    BIND(sio:SIO_000128 AS ?relationship)
  }
  UNION
  {
    she:Metals sio:SIO_000128 ?source .
    she:Metals skos:prefLabel ?pollutantLabel .
    ?source skos:prefLabel ?sourceLabel .
    FILTER(?source != she:ArableSoils)
    BIND(sio:SIO_000128 AS ?relationship)
  }
}
ORDER BY ?pollutantLabel ?sourceLabel