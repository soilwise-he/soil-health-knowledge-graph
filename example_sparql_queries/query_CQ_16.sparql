PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX she: <https://soilwise-he.github.io/soil-health#>
PREFIX obo: <http://purl.obolibrary.org/obo/>

SELECT DISTINCT ?subject ?predicate ?object ?objectLabel
WHERE {
  ?sedConcept skos:prefLabel "soil ecological degradation"@en .

  {
    ?sedConcept skos:definition ?definitionText .
    BIND(?sedConcept AS ?subject)
    BIND(skos:definition AS ?predicate)
    BIND(?definitionText AS ?object)
    BIND("" AS ?objectLabel)
  }
  UNION
  {
    ?sedConcept she:reflects ?reflectedConcept .
    ?reflectedConcept skos:prefLabel ?reflectedConceptLabel .
    BIND(?sedConcept AS ?subject)
    BIND(she:reflects AS ?predicate)
    BIND(?reflectedConcept AS ?object)
    BIND(?reflectedConceptLabel AS ?objectLabel)
  }
  UNION
  {
    ?sedConcept obo:RO_0002552 she:EcosystemFunctions .

    she:EcosystemFunctions skos:narrower ?narrowerFunction .
    ?narrowerFunction skos:prefLabel ?narrowerFunctionLabel .

    BIND(?sedConcept AS ?subject)
    BIND(obo:RO_0002552 AS ?predicate)
    BIND(?narrowerFunction AS ?object)
    BIND(?narrowerFunctionLabel AS ?objectLabel)
  }
}
ORDER BY ?subject ?predicate ?objectLabel