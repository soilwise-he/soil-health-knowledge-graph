PREFIX agrontology: <http://aims.fao.org/aos/agrontology#>
PREFIX biolink: <https://w3id.org/biolink/vocab/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX qudt: <http://qudt.org/schema/qudt/>
PREFIX she: <https://soilwise-he.github.io/soil-health#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sorelm: <http://sweetontology.net/relaMath/>
PREFIX sorelsp: <http://sweetontology.net/relaSpace/>
PREFIX unit: <http://qudt.org/vocab/unit/>

SELECT DISTINCT 
    ?groupLabel 
    ?definition
    ?sizeUnit ?sizeValue
    ?diameterUnit ?diameterInterval
    ?lengthUnit ?lengthInterval
    ?biomassUnit ?biomassInterval ?biomassUpperBound ?biomassValue
    ?densityUnit ?densityInterval ?densityUpperBound ?densityValue
    ?freshBiomassUnit ?freshBiomassUpperBound
    ?feedingHabit
    ?consumedItem
    ?narrowerItem
    ?component
WHERE {
    she:FunctionalGroups skos:narrower ?functionalGroup .
    ?functionalGroup skos:prefLabel ?groupLabel .
    
    OPTIONAL { ?functionalGroup skos:definition ?definition }
    
    OPTIONAL {
        ?functionalGroup obo:MICRO_0000105 ?sizeBlank .
        ?sizeBlank qudt:numericValue ?sizeValue ;
                   qudt:unit ?sizeUnit .
    }
    
    OPTIONAL {
        ?functionalGroup sorelsp:diameter ?diameterBlank .
        ?diameterBlank qudt:unit ?diameterUnit ;
                      sorelm:hasInterval ?diameterInterval .
    }
    
    OPTIONAL {
        ?functionalGroup sorelsp:hasLength ?lengthBlank .
        ?lengthBlank qudt:unit ?lengthUnit ;
                    sorelm:hasInterval ?lengthInterval .
    }
    
    OPTIONAL {
        ?functionalGroup she:hasBiomass ?biomassBlank .
        ?biomassBlank qudt:unit ?biomassUnit .
        OPTIONAL { ?biomassBlank sorelm:hasInterval ?biomassInterval }
        OPTIONAL { ?biomassBlank sorelm:hasUpperBound ?biomassUpperBound }
        OPTIONAL { ?biomassBlank qudt:numericValue ?biomassValue }
    }
    
    OPTIONAL {
        ?functionalGroup she:hasDensity ?densityBlank .
        ?densityBlank qudt:unit ?densityUnit .
        OPTIONAL { ?densityBlank sorelm:hasInterval ?densityInterval }
        OPTIONAL { ?densityBlank sorelm:hasUpperBound ?densityUpperBound }
        OPTIONAL { ?densityBlank qudt:valueSN ?densityValue }
    }
    
    OPTIONAL {
        ?functionalGroup she:hasFreshBiomass ?freshBiomassBlank .
        ?freshBiomassBlank qudt:unit ?freshBiomassUnit ;
                          sorelm:hasUpperBound ?freshBiomassUpperBound .
    }
    
    OPTIONAL {
        ?functionalGroup skos:broader ?feedingHabit .
        FILTER(?feedingHabit IN (she:Bacterivores, she:Fungivores, she:Herbivores, 
                                she:Omnivores, she:Predators))
    }
    
    OPTIONAL {
        ?functionalGroup biolink:consumes ?consumedItem .
    }

     OPTIONAL {
        ?functionalGroup skos:narrower ?narrowerItem .
    }
    
    OPTIONAL {
        ?functionalGroup agrontology:hasComponent ?component .
    }
}
ORDER BY ?groupLabel