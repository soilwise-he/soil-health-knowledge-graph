PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX she: <https://soilwise-he.github.io/soil-health#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX af-x: <http://purl.allotrope.org/ontologies/property#>
PREFIX agrontology: <http://aims.fao.org/aos/agrontology#>

SELECT ?itemDescription ?relationshipURI ?valueDescription
WHERE {
  {
    she:SoilPConcentration skos:prefLabel ?pConcLabel .
    she:SoilPConcentration obo:RO_0002334 ?bnode .
    ?bnode rdf:object ?pStock .
    ?pStock skos:prefLabel ?pStockLabel .
    ?bnode af-x:AFX_0000408 she:SoilSolution .
    she:SoilSolution skos:prefLabel ?soilSolutionLabel .

    BIND(?pConcLabel AS ?itemDescription)
    BIND(obo:RO_0002334 AS ?relationshipURI)
    BIND(CONCAT(?pStockLabel, " (in ", ?soilSolutionLabel, ")") AS ?valueDescription)
  }
  UNION
  {
    she:SoilPAvailability skos:prefLabel ?pAvailLabel .
    ?affectingParameter agrontology:affects she:SoilPAvailability .
    ?affectingParameter skos:prefLabel ?affectingParameterLabel .
    FILTER(?affectingParameter IN (she:SoilParameters, she:AluminiumOxideContent, she:IronOxideContent))

    BIND(?affectingParameterLabel AS ?itemDescription)
    BIND(agrontology:affects AS ?relationshipURI)
    BIND(?pAvailLabel AS ?valueDescription)
  }
}
ORDER BY ?itemDescription ?relationshipURI