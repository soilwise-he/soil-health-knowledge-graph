PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX she: <https://soilwise-he.github.io/soil-health#>
PREFIX qudt: <http://qudt.org/schema/qudt/>
PREFIX unit: <http://qudt.org/vocab/unit/>
PREFIX om: <http://www.ontology-of-units-of-measure.org/resource/om-2/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX agrontology: <http://aims.fao.org/aos/agrontology#>

SELECT ?parameterLabel ?propertyURI ?valueLabel
WHERE {
  she:ComplexSoilPhysicalParameters skos:narrower ?parameterURI .
  ?parameterURI skos:prefLabel ?parameterLabel .

  {
    ?parameterURI qudt:unit ?unitValueURI .
    OPTIONAL { ?unitValueURI skos:prefLabel ?unitPrefLabel . }
    BIND(
      COALESCE(
        ?unitPrefLabel,
        IF( STRSTARTS(STR(?unitValueURI), STR(unit:)),
            STRAFTER(STR(?unitValueURI), STR(unit:)),
            IF( STRSTARTS(STR(?unitValueURI), STR(om:)),
                STRAFTER(STR(?unitValueURI), STR(om:)),
                STR(?unitValueURI)
            )
        )
      ) AS ?unitDisplayLabel
    )

    BIND(qudt:unit AS ?propertyURI)
    BIND(?unitDisplayLabel AS ?valueLabel)
  }
  UNION
  {
    ?parameterURI qudt:symbol ?symbolValue .
    BIND(qudt:symbol AS ?propertyURI)
    BIND(STR(?symbolValue) AS ?valueLabel)
  }
  UNION
  {
    FILTER(?parameterURI = she:SoilRigidityRatio)
    ?parameterURI agrontology:isMeasuredBy ?measureNode .
    ?measureNode obo:RO_0020202 ?numerator .
    ?measureNode obo:RO_0020203 ?denominator .
    ?numerator skos:prefLabel ?numeratorLabel .
    ?denominator skos:prefLabel ?denominatorLabel .

    BIND(agrontology:isMeasuredBy AS ?propertyURI)
    BIND(CONCAT(?numeratorLabel, " / ", ?denominatorLabel) AS ?valueLabel)
  }
  UNION
  {
    FILTER(?parameterURI = she:ShearStrength)
    ?parameterURI sio:equivalentTo she:Stiffness .
    she:Stiffness skos:prefLabel ?stiffnessLabel .
    BIND(sio:equivalentTo AS ?propertyURI)
    BIND(?stiffnessLabel AS ?valueLabel)
  }
}
ORDER BY ?parameterLabel ?propertyURI