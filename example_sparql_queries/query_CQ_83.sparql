PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX she: <https://soilwise-he.github.io/soil-health#>
PREFIX agrontology: <http://aims.fao.org/aos/agrontology#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX sio: <http://semanticscience.org/resource/>

SELECT ?subjectLabel ?predicateURI ?objectLabel
WHERE {
  {
    she:SoilCompaction skos:prefLabel ?scLabel_1 .
    she:SoilCompaction obo:RO_0002552 ?sps .
    ?sps skos:prefLabel ?spsLabel .
    BIND(?scLabel_1 AS ?subjectLabel)
    BIND(obo:RO_0002552 AS ?predicateURI)
    BIND(?spsLabel AS ?objectLabel)
  }
  UNION
  {
    she:SoilCompaction skos:prefLabel ?scLabel_2 .
    she:SoilCompaction obo:RO_0002212 ?reducedPhysicalProperty .
    ?reducedPhysicalProperty skos:prefLabel ?rppLabel .
    BIND(?scLabel_2 AS ?subjectLabel)
    BIND(obo:RO_0002212 AS ?predicateURI)
    BIND(?rppLabel AS ?objectLabel)
  }
  UNION
  {
    she:SoilCompaction skos:prefLabel ?scLabel_5 .
    she:SoilCompaction obo:RO_0002213 ?wl .
    ?wl skos:prefLabel ?wlLabel .
    BIND(?scLabel_5 AS ?subjectLabel)
    BIND(obo:RO_0002213 AS ?predicateURI)
    BIND(?wlLabel AS ?objectLabel)
  }
  UNION
  {
    she:SoilCompaction skos:prefLabel ?scLabel_6 .
    ?sbq agrontology:isAffectedBy she:SoilCompaction .
    ?sbq skos:prefLabel ?sbpLabel .
    BIND(?scLabel_6 AS ?subjectLabel)
    BIND(agrontology:affects AS ?predicateURI)
    BIND(?sbpLabel AS ?objectLabel)
  }
  UNION
  {
    ?ghg agrontology:isAffectedBy she:SoilCompaction .
    ?ghg skos:prefLabel ?ghgLabel .
    ?specificGHG obo:RO_0002331 ?ghg .
    ?specificGHG skos:prefLabel ?specificGHGLabel .

    BIND(?specificGHGLabel AS ?subjectLabel)
    BIND(obo:RO_0002331 AS ?predicateURI)
    BIND(?ghgLabel AS ?objectLabel)
  }
  UNION
  {
    she:SoilCompaction skos:prefLabel ?scLabel_9 .
    ?productionType obo:RO_0002335 she:SoilCompaction .
    ?productionType skos:prefLabel ?productionTypeLabel .
    BIND(?productionTypeLabel AS ?subjectLabel)
    BIND(obo:RO_0002335 AS ?predicateURI)
    BIND(?scLabel_9 AS ?objectLabel)
  }
  UNION
  {
    she:SoilCompaction skos:prefLabel ?scLabel_10 .
    she:SoilCompaction agrontology:isCausedBy ?causeNode .
    ?causeNode sio:SIO_000635 ?csml .
    ?csml skos:prefLabel ?csmlLabel .
    OPTIONAL {
        ?causeNode agrontology:makeUseOf ?hm .
        ?hm skos:prefLabel ?hmLabel .
    }
    BIND(?scLabel_10 AS ?subjectLabel)
    BIND(agrontology:isCausedBy AS ?predicateURI)
    BIND(IF(BOUND(?hmLabel), CONCAT("operations at ", ?csmlLabel, " using ", ?hmLabel), CONCAT("Operations at ", ?csmlLabel)) AS ?objectLabel)
  }
}
ORDER BY ?subjectLabel ?predicateURI ?objectLabel